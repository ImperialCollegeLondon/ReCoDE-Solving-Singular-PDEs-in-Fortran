# 02. Fortran Package Manager

The Fortran Package Manager (FPM) is a modern build system and package manager specifically designed for Fortran. 
FPM aims to simplify the process of building and testing Fortran software by providing tools and conventions that streamline these tasks.
We have used FPM to help build our project.

Key Features of FPM:
  - Easy Project Creation: FPM makes it straightforward to create new Fortran projects with standardized directory structures and documentation.

  - Dependency Management: FPM handles dependencies between Fortran packages, making it easier to include external libraries and manage their versions.

  - Building and Compiling: It automates the compilation process, ensuring that Fortran code is compiled with the appropriate flags and settings.

  - Testing: FPM supports automated testing, which helps in maintaining code quality and reliability.


## How to install FPM

See [here](https://fpm.fortran-lang.org/install/index.html) for a guide on how to install FPM. 

## How to use FPM

We have created a directory in the repository called `fortran_fibonacci`. In your terminal open this directory. Inside this directory we have a very simple Fortran code built with FPM. The FPM repository was initiated with the command `fpm new fortran_fibonacci` and following the guide [here](https://fpm.fortran-lang.org/tutorial/hello-fpm.html).

The structure of the repository is given by
```
├── README.md
├── app
│   └── main.f90
├── fpm.toml
├── src
│   └── fibonacci.f90
├── LICENSE
└── test
    └── check.f90
```
The `README.md` file describes how to set up the program.
The important commands when using FPM are: 
  - `fpm run` which compiles and runs the code
  - `fpm test` which complies and runs the test
  - `fpm help` which prints to screen all the possible FPM commands

The other important file is `fpm.toml`:
```
name = "fibonacci"
version = "0.1.0"
license = "license"
author = "Diego Alonso Álvarez"
maintainer = "d.alonso-alvarez@imperial.ac.uk"
copyright = "Copyright 2024, Diego Alonso Álvarez"

[build]
auto-executables = true
auto-tests = true
auto-examples = true
module-naming = false

[install]
library = false

[fortran]
implicit-typing = false
implicit-external = false
source-form = "free"

[dev-dependencies]
test-drive.git = "https://github.com/fortran-lang/test-drive"
test-drive.tag = "v0.4.0"
```

For now, the important sections are `[fortran]`, which sets the default compiler settings and `[dev-dependencies]` which importants the testing framework.


## Testing

Here is the testing module for the program. It has a module filed with tests, `test_demo`, and a program that calls and runs the tests, `tester`.

Starting with `test_demo`, there are three tests: `test_negative`, `test_0_or_1` and `test_greater_than_1`. All these subroutines have an `error` as an output. The error is calculated with the intrinsic `check`; for example on line `106`, `call check(error, fib(-6), -1)` checks if `fib(-6)==-1` and returns an error if it is not.

These three tests are collected in the subroutine `collect_tests` and included in the `testsuite` unit test. Each `new_unittest(string, test)` has a string input that gets printed to the screen when the test is run, and a subroutine test that only returns an error. 

The program `tester` then calls the `testsuite` and reports any error it finds. 


```
module test_demo
    use fibonacci, only: fib
    use testdrive, only: error_type, unittest_type, new_unittest, check
    implicit none
    private

    public :: collect_tests

contains

    !> Collect all exported unit tests
    subroutine collect_tests(testsuite)
        !> Collection of tests
        type(unittest_type), allocatable, intent(out) :: testsuite(:)

      testsuite = [ &
        new_unittest("test_negative", test_negative),  &
        new_unittest("test_0_or_1", test_0_or_1), &
        new_unittest("input_greater_than_1", test_greater_than_1) &
        ]
    end subroutine collect_tests

    !> Check input is negative
    subroutine test_negative(error)
      !> Error handling
      type(error_type), allocatable, intent(out) :: error
      call check(error, fib(-6), -1)

    end subroutine test_negative

    !> Check input is zero or one
    subroutine test_0_or_1(error)
        !> Error handling
        type(error_type), allocatable, intent(out) :: error
        call check(error, fib(0), 0)
        call check(error, fib(1), 1)

    end subroutine test_0_or_1

    !> Check input is grerater than one
    subroutine test_greater_than_1(error)
      !> Error handling
      type(error_type), allocatable, intent(out) :: error
      integer :: n

      do n = 2, 10
          call check(error, fib(n), fib(n-1) + fib(n-2))
      end do

  end subroutine test_greater_than_1

end module test_demo

program tester
  use, intrinsic :: iso_fortran_env, only : error_unit
  use testdrive, only : run_testsuite
  use test_demo, only : collect_tests
  implicit none
  integer :: stat

  stat = 0
  call run_testsuite(collect_tests, error_unit, stat)

  if (stat > 0) then
    write(error_unit, '(i0, 1x, a)') stat, "test(s) failed!"
    error stop
  end if
end program tester
```







