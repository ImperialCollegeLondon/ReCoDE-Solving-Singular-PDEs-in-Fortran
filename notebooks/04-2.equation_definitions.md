# 04-2 Running the code

Here we will discuss how to run and edit the code. In this section we will discuss how the equations are set in the file `equations/definitions.f90`.
Depending on the settings set in `settings.input`, different subroutines will be called.



### `equations/definitions.f90` Explaination

The first subroutine to consider is `equation1_linear`. It sets up terms of the linear equation:

$$ A_x(x,y) \frac{\partial^2 u}{\partial x^2} + B_x(x,y) \frac{\partial u}{\partial x } + A_y(x,y) \frac{\partial^2 u}{\partial y^2} + B_y(x,y) \frac{\partial u}{\partial y } + C(x,y) u = D(x,y) \frac{\partial u }{\partial t}$$

This is subroutine will always called regardless of `Eqn_number` being 1 or 2; if this value is 2 then `equation2_linear` will also be called. The subroutine `equation2_linear` is exactly the same but solves for $v$ rather than $u$.
`Ax`, `Bx`, `C` and `D` will always be set. `Ay` and `By` will only be set if `Domain_number` is 2.

```
!!
! @brief      Sets up the first equation in the form 
!              Ax u_xx + Bx u_x + Ay u_yy + By u_y + C u = D or D u_t
!
! @param       x     physical domain input - can have variable coefficients with x
! @param       y     physical domain input - can have variable coefficients with y
!
! @return      Ax     second order x derivative coefficient
! @return      Bx     first order x derivative coefficient
! @return      Ay     second order y derivative coefficient
! @return      By     first order y derivative coefficient
! @return      C     zeroth order derivative coefficient
! @return      D     RHS/inhomogenous term
!
!!
   Subroutine equation1_linear(x, y, Ax, Bx, Ay, By, C, D)
      real(dp), intent(in) :: x, y ! xpoisiton in the domain
      real(dp), intent(out) :: Ax, Bx, Ay, By, C, D
      real(dp) :: epsi

      epsi = 0.05d0

      Ax = epsi
      Bx = 0.d0

      Ay = epsi
      By = 0.d0
      
      C = 0.d0
      D = 1.d0

   End Subroutine equation1_linear
```

This subroutine `equation1_BC_X_Bot` is a boundary condition and is identical in form to the one above. 
However it is only called when $x$ is at the lower boundary `xl`. 
There are four boundary condition subroutines for each equation: 

   For $u$: 
   - `equation1_BC_X_Bot` and `equation1_BC_X_Top` are always called and give conditions for the `xl` and `xr` boundaries respectively
   - `equation1_BC_Y_Bot` and `equation1_BC_Y_Top` are only called when `Domain_number` is 2, and give conditions for the `yl` and `yr` boundaries respectively
   For $v$ (only called when `Eqn_number` is 2):
   - `equation2_BC_X_Bot` and `equation2_BC_X_Top` are always called and give conditions for the `xl` and `xr` boundaries respectively
   - `equation2_BC_Y_Bot` and `equation2_BC_Y_Top` are only called when `Domain_number` is 2, and give conditions for the `yl` and `yr` boundaries respectively
   
```
!!
! @brief      Sets up the bottom/lefthand boundary in the first equation in the form 
!             Ax u_xx + Bx u_x + Ay u_yy + By u_y + C u = D or D u_t
!             
!             When x = xl
!
! @param       x     physical domain input - can have variable coefficients with x
! @param       y     physical domain input - can have variable coefficients with y
!
! @return      Ax     second order x derivative coefficient
! @return      Bx     first order x derivative coefficient
! @return      Ay     second order y derivative coefficient
! @return      By     first order y derivative coefficient
! @return      C     zeroth order derivative coefficient
! @return      D     RHS/inhomogenous term
!
!!
   Subroutine equation1_BC_X_Bot(x, y, Ax, Bx, Ay, By, C, D)
      real(dp), intent(in) :: x, y ! xpoisiton in the domain
      real(dp), intent(out) :: Ax, Bx, Ay, By, C, D

      Ax = 0.d0
      Bx = 1.d0

      Ay = 0.d0
      By = 0.d0
      
      C = 0.d0
      D = 0.d0

   End Subroutine equation1_BC_X_Bot

```
When `Time_switch` is 1, the following is called to set the initial condition. There is a similar subroutine for $v$ that is called if `Eqn_number` is 2.

```
!!
! @brief      Sets up the the initial conidiotn in the form u = IC. Note in 1D y = 1
!
! @param       x     physical domain input - can have variable coefficients with x
! @param       y     physical domain input - can have variable coefficients with y
!
! @return      IC     Initial condition
!
!!
   Subroutine equation1_initial_condition(x, y, IC)
      real(dp), intent(in) :: x, y ! xpoisiton in the domain
      real(dp), intent(out) :: IC

      IC = x + y 

   End Subroutine equation1_initial_condition

```

When `Non_linear` is 1, the following is called to set the non-linear terms. There is a similar subroutine for $v$ that is called if `Eqn_number` is 2.
If `Eqn_number` is 1 then only `F` and `Fu` are called.
If `Eqn_number` is 2 then `F`, `Fu` and `Fv` are called.

```
!!
! @brief      Sets up the nonlinear terms F in the equation 
!             Ax u_xx + Bx u_x + Ay u_yy + By u_y + C u + F(u,v) = D or D u_t
!
! @param       x     physical domain input - can have variable coefficients with x
! @param       y     physical domain input - can have variable coefficients with y
! @param       u     non-linear variable
! @param       v     non-linear variable
!
! @return      F     Non-linear output (F is a function of u and v)
! @return      Fu    u - Partial Derivative of Non-linear output (F is a function of u and v)
! @return      Fv    v - Partial Derivative of Non-linear output (F is a function of u and v)
!
!!
   Subroutine equation1_non_linear(x, y, u, v, F, Fu, Fv)
      real(dp), intent(in) :: x, y, u, v
      real(dp), intent(out) :: F, Fu, Fv
      !!! F is a function of u - Fu is dF(u,v)/du and Fv is dF(u,v)/dv
      !!!   Non linear terms do not effect boundaries

      F = u*u*v - u
      Fu = 2.d0*u*v - 1.d0 
      Fv = u*u
   End Subroutine equation1_non_linear
```
